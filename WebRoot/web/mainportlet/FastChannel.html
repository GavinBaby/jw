<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
<meta http-equiv="Content-Type" content="text/html; charset=GBK" />
<title></title>
<!--[if lt IE 9]>
<script src="http://html5shiv.googlecode.com/svn/trunk/html5.js"></script>
<![endif]-->
</head>

<body>

<style type="text/css">
*{margin:0;padding:0;}
html{background-color:#ffffff;}
body{color:#fff;font-family:Arial;}
a, a:visited {text-decoration:none;outline:none;color:#54a6de;}
a:hover{text-decoration:underline;}
#mask{width:250px;height:153px;position:relative;overflow:hidden;auto 0;}
#allScreens{height:100%;top:0;left:0;position:absolute;cursor:move;}
.screen{width:332px;float:left;}
#dock .dockicon,.screen .icon{float:left;width:80px;height:75px;background-repeat:no-repeat;margin:2px;position:relative;}
</style>

<div id="homeScreen">
	<div id="mask">
		<div id="allScreens"></div>
	</div>
	
	
</div>

<script src="../../js/jquery1.6.2.js"></script>
<script src="../../js/coffee/touchable.js"></script>
<script src="../../js/coffee/coffee-script.js"></script>

<script type="text/coffeescript">
	
	# The Icon class. 
	
	class Icon

		# The constructor. The -> arrow signifies
		# a function definition.
		
		constructor: (@id, @title, @url) ->
			# @ is synonymous for "this". The id and title parameters
			# of the construtor are automatically added as this.id and this.title
			
			# @markup holds the HTML of the icon. It is
			# transformed to this.markup behind the scenes.
			
			@markup = "<div class='icon' style='background-image:url(../../images/coffeeicons/#{@id}.png)'
						 title='#{@title}' ondblclick='javascript:alert()'></div>"
	

	# The DockIcon class inherits from Icon
	
	class DockIcon extends Icon
		constructor: (id, title, @url)->

			# This calls the constructor if Icon
			
			super(id, title, @url)
			
			# Changing the class name of the generated HTML 
			@markup = @markup.replace("class='icon'","class='dockicon'")
	
	
	# The Screen Class
	
	class Screen
	
		# Function arguments can have default parameters
		constructor: (icons = [])->
			@icons = icons
		
		attachIcons: (icons = [])->
			Array.prototype.push.apply(@icons, icons)

		generate: ->
			markup = []
			
			# Looping through the @icons array
			markup.push(icon.markup) for icon in @icons
			
			# The last line of every function is implicitly returned
			"<div class='screen'>#{markup.join('')}</div>"
		

	class Stage
	
		# The width of our "device" screen. This is 
		# basically the width of the #mask div.
		
		screenWidth: 332
	
		constructor: (icons)->
			
			@currentScreen = 0
			@screens = []
			
			# Calculating the number of screens
			# necessary to display all the icons
			
			num = Math.ceil(icons.length / 6)
			i = 0
			
			while num--
				# we pass a slice of the icons array
				s = new Screen(icons[i...i+6])
				
				# adding the screen to the local screens array
				@screens.push(s)
				
				i+=6
		
		# This method populates the passed element with HTML
		addScreensTo: (element)->
		
			@element = $(element)
			@element.width(@screens.length*@screenWidth)
			
			for screen in @screens
				@element.append(screen.generate())
				
		addIndicatorsTo: (elem)->
		
			# This method creates the small
			# circular indicatiors
			 
			@ul = $(elem)
			
			for screen in @screens
				@ul.append('<li>')
			
			@ul.find('li:first').addClass('active');
			
		goTo: (screenNum)->
		
			# This method animates the allScreen div in
			# order to expose the needed screen in #mask
			
			if @element.is(':animated')
				return false
			
			# if this is the first or last screen,
			# run the end of scroll animation
			
			if @currentScreen == screenNum
			
				# Parallel assignment:
				[from, to] = ['+=15','-=15']
				
				if @currentScreen != 0
					[from, to] = [to, from]
				
				@element.animate( { marginLeft : from }, 150 )
						.animate( { marginLeft : to }, 150 )
			else
				# If everything is ok, animate the transition between the screens.
				
				# The fat arrow => preserves the context of "this"
				
				@element.animate( { marginLeft:-screenNum*@screenWidth }, => @currentScreen = screenNum )
				@ul.find('li').removeClass('active').eq(screenNum).addClass('active');
				
		next: ->
			toShow = @currentScreen+1
			
			# If there is no next screen, show
			# the current one
			
			if toShow == @screens.length
				toShow = @screens.length - 1
			
			@goTo(toShow)
			
		previous: ->
			toShow = @currentScreen-1
			
			# If there is no previous screen,
			# show the current one
			
			if toShow == -1
				toShow = 0
			
			@goTo(toShow)
	
	# This is equivalent to $('document').ready(function(){}):
	
	$ ->
	
		allIcons = [
			new Icon('blue_fwgl', '发文管理'), new Icon('blue_swgl', '收文管理')
			new Icon('blue_ldps', '领导批示'), new Icon('blue_rcap', '日程安排')
			new Icon('blue_fwgl', 'Weather'), new Icon('blue_fwgl', 'NES Emulator')
			new Icon('blue_fwgl', 'Calendar'), new Icon('blue_fwgl', 'Clock')
			new Icon('blue_fwgl', 'Boss Prefs'), new Icon('blue_fwgl', 'Chess')
			new Icon('blue_fwgl', 'Mail'), new Icon('blue_fwgl', 'Phone')
			new Icon('blue_fwgl', 'SMS Center'), new Icon('blue_fwgl', 'Camera')
			new Icon('blue_fwgl', 'iPod'), new Icon('blue_fwgl', 'Calculator')
			new Icon('blue_fwgl', 'Music'), new Icon('blue_fwgl', 'Poof')
			new Icon('Settings', 'Settings'), new Icon('blue_fwgl', 'Youtube')
			new Icon('psx4all', 'PSx4All'), new Icon('blue_fwgl', 'Record Video')
			new Icon('Installer', 'Installer'), new Icon('blue_fwgl', 'Notes')
			new Icon('RagingThunder', 'RagingThunder'), new Icon('blue_fwgl', 'Stocks')
			new Icon('genesis4iphone', 'Genesis'), new Icon('blue_fwgl', 'SNES Emulator')
			new Icon('Calendar', 'Calendar'), new Icon('blue_fwgl', 'Clock')
			new Icon('Photos', 'Photo Gallery'), new Icon('blue_fwgl', 'Google Maps')
		]
	
	
		allScreens = $('#allScreens')
		
		# Using the Touchable plugin to listen for
		# touch based events:
		
		allScreens.Touchable();


		# Creating a new stage object
		stage = new Stage(allIcons)
		
		stage.addScreensTo(allScreens)
		stage.addIndicatorsTo('#indicators')
		
		# Listening for the touchablemove event.
		# Notice the callback function
		
		allScreens.bind 'touchablemove', (e,touch)->	
			stage.next() if touch.currentDelta.x < -5
			stage.previous() if touch.currentDelta.x > 5
		
		
		# Adding the dock icons:
		
		dock = $('#dock')
		
		for icon in dockIcons
			dock.append(icon.markup)
		
			
</script>

</body>
</html>